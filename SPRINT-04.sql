/* =======================================================================
   CHALLENGER 2025 – SPRINT 04  
   - RESET completo do schema (drop seguro de tudo do usuário)
   - DDL (tabelas, constraints, índices)
   - PACKAGE de regras + WRAPPERS + TRIGGERS de auditoria
   - CARGA determinística (15 setores, 15 motos, 45 movs com datas/custos fixos)
   - EXPORT JSON "linha a linha" em CH_JSON_OUT    
   - Blocos SPOOL prontos 
======================================================================= */

SET SERVEROUTPUT ON
SET DEFINE OFF
ALTER SESSION SET nls_date_format = 'YYYY-MM-DD"T"HH24:MI:SS';

PROMPT == INICIO SPRINT 04 ==

/* ============================== [01] RESET ============================
   Limpa o schema do usuário    
   ===================================================================== */
DECLARE
  PROCEDURE try_exec(p_sql VARCHAR2) IS
  BEGIN
    EXECUTE IMMEDIATE p_sql;
  EXCEPTION WHEN OTHERS THEN NULL;
  END;
BEGIN
  -- desabilita triggers
  FOR r IN (SELECT trigger_name FROM user_triggers) LOOP
    try_exec('ALTER TRIGGER '||r.trigger_name||' DISABLE');
  END LOOP;

  -- triggers
  FOR r IN (SELECT trigger_name FROM user_triggers) LOOP
    try_exec('DROP TRIGGER '||r.trigger_name);
  END LOOP;

  -- views
  FOR r IN (SELECT view_name FROM user_views) LOOP
    try_exec('DROP VIEW '||r.view_name);
  END LOOP;

  -- procs / funcs / packages
  FOR r IN (SELECT object_name FROM user_objects WHERE object_type='PROCEDURE') LOOP
    try_exec('DROP PROCEDURE '||r.object_name);
  END LOOP;
  FOR r IN (SELECT object_name FROM user_objects WHERE object_type='FUNCTION') LOOP
    try_exec('DROP FUNCTION '||r.object_name);
  END LOOP;
  FOR r IN (SELECT object_name FROM user_objects WHERE object_type='PACKAGE BODY') LOOP
    try_exec('DROP PACKAGE BODY '||r.object_name);
  END LOOP;
  FOR r IN (SELECT object_name FROM user_objects WHERE object_type='PACKAGE') LOOP
    try_exec('DROP PACKAGE '||r.object_name);
  END LOOP;

  -- types 
  FOR r IN (SELECT object_name FROM user_objects WHERE object_type='TYPE BODY') LOOP
    try_exec('DROP TYPE BODY '||r.object_name);
  END LOOP;
  FOR r IN (SELECT object_name FROM user_objects WHERE object_type='TYPE') LOOP
    try_exec('DROP TYPE '||r.object_name||' FORCE');
  END LOOP;

  -- sequences criadas pelo usuário (mantém as ISEQ$$_ das identidades)
  FOR r IN (SELECT sequence_name FROM user_sequences WHERE sequence_name NOT LIKE 'ISEQ$$_%') LOOP
    try_exec('DROP SEQUENCE '||r.sequence_name);
  END LOOP;

  -- tables (tento com e sem aspas)
  FOR r IN (SELECT table_name FROM user_tables) LOOP
    try_exec('DROP TABLE "'||r.table_name||'" CASCADE CONSTRAINTS PURGE');
    try_exec('DROP TABLE '||r.table_name||' CASCADE CONSTRAINTS PURGE');
  END LOOP;

  DBMS_OUTPUT.PUT_LINE('..reset ok');
END;
/
PROMPT ..reset concluido

/* =============================== [02] DDL =============================
   Tabelas do domínio, constraints e índices
   ===================================================================== */
CREATE TABLE CH_SETOR (
  ID_SETOR   NUMBER GENERATED BY DEFAULT AS IDENTITY,
  NOME_SETOR VARCHAR2(60) NOT NULL,
  ATIVO      CHAR(1) DEFAULT 'S' NOT NULL,
  CONSTRAINT PK_CH_SETOR PRIMARY KEY (ID_SETOR),
  CONSTRAINT UQ_CH_SETOR_NOME UNIQUE (NOME_SETOR),
  CONSTRAINT CK_CH_SETOR_ATIVO CHECK (ATIVO IN ('S','N'))
);

CREATE TABLE CH_MOTOCICLETA (
  ID_MOTO     NUMBER GENERATED BY DEFAULT AS IDENTITY,
  PLACA       VARCHAR2(8)  NOT NULL,
  MODELO      VARCHAR2(60) NOT NULL,
  STATUS      VARCHAR2(15) DEFAULT 'ATIVA' NOT NULL, 
  DT_CADASTRO DATE DEFAULT SYSDATE NOT NULL,
  CONSTRAINT PK_CH_MOTO PRIMARY KEY (ID_MOTO),
  CONSTRAINT UQ_CH_MOTO_PLACA UNIQUE (PLACA),
  CONSTRAINT CK_CH_MOTO_STATUS CHECK (STATUS IN ('ATIVA','MANUTENCAO','INATIVA'))
);

CREATE TABLE CH_MOVIMENTACAO (
  ID_MOV      NUMBER GENERATED BY DEFAULT AS IDENTITY,
  ID_MOTO     NUMBER NOT NULL,
  ID_SETOR    NUMBER NOT NULL,
  TIPO_MOV    VARCHAR2(10) NOT NULL,        
  DT_MOV      DATE NOT NULL,
  CUSTO       NUMBER(10,2) DEFAULT 0 NOT NULL,
  OBSERVACAO  VARCHAR2(400),
  CONSTRAINT PK_CH_MOV PRIMARY KEY (ID_MOV),
  CONSTRAINT FK_CH_MOV_MOTO  FOREIGN KEY (ID_MOTO)  REFERENCES CH_MOTOCICLETA(ID_MOTO),
  CONSTRAINT FK_CH_MOV_SETOR FOREIGN KEY (ID_SETOR) REFERENCES CH_SETOR(ID_SETOR),
  CONSTRAINT CK_CH_MOV_TIPO  CHECK (TIPO_MOV IN ('ENTRADA','SAIDA','TRANSF')),
  CONSTRAINT CK_CH_MOV_CUSTO CHECK (CUSTO >= 0)
);

CREATE TABLE CH_AUDITORIA (
  ID_AUDITORIA NUMBER GENERATED BY DEFAULT AS IDENTITY,
  TABELA       VARCHAR2(30) NOT NULL,
  OPERACAO     VARCHAR2(10) NOT NULL, 
  ID_REGISTRO  NUMBER,
  DETALHE      VARCHAR2(4000),
  DATA_AUD     DATE DEFAULT SYSDATE NOT NULL,
  USUARIO_BD   VARCHAR2(128) DEFAULT USER NOT NULL,
  CONSTRAINT PK_CH_AUD PRIMARY KEY (ID_AUDITORIA)
);

CREATE INDEX IX_CH_MOV_MOTO  ON CH_MOVIMENTACAO(ID_MOTO);
CREATE INDEX IX_CH_MOV_SETOR ON CH_MOVIMENTACAO(ID_SETOR);
CREATE INDEX IX_CH_MOV_DATA  ON CH_MOVIMENTACAO(DT_MOV);

PROMPT ..ddl criado

/* =============================== [03] CARGA ===========================
   15 setores, 15 motos, 45 movimentações (datas/custos determinísticos)
   Datas base: 2025-09-29 .. 2025-10-13 (15 dias)
   Custos: 
     - Moto 1 (Recepcao, ENTRADA)
     - Moto 6 (Entrega, TRANSF)
     - Moto 11 (Comercial, SAIDA)
   ===================================================================== */
-- zera (idempotente)
DELETE FROM CH_MOVIMENTACAO;
DELETE FROM CH_MOTOCICLETA;
DELETE FROM CH_SETOR;
COMMIT;

-- 03.1 Setores 
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Recepcao','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Entrega','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Comercial','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Financeiro','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Oficina','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Estoque','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('RH','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Compras','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('TI','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Juridico','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Diretoria','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Marketing','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Qualidade','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Seguranca','S');
INSERT INTO CH_SETOR (NOME_SETOR, ATIVO) VALUES ('Logistica','S');

-- 03.2 Motos 
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('AAA1A11','CG 160 Fan','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('BBB2B22','NMax 160','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('CCC3C33','Biz 125','MANUTENCAO');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('DDD4D44','CB 300F','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('EEE5E55','Fazer 250','INATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('FFF6F66','YBR 150','ATIVA');     -- ID 6
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('GGG7G77','PCX 160','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('HHH8H88','Hornet 600','MANUTENCAO');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('III9I99','CB Twister','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('JJJ0J00','Bros 160','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('KKK1K12','MT 03','ATIVA');       -- ID 11
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('LLL2L23','Factor 150','INATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('MMM3M34','Pop 110i','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('NNN4N45','CBR 650R','ATIVA');
INSERT INTO CH_MOTOCICLETA (PLACA, MODELO, STATUS) VALUES ('OOO5O56','XRE 300','MANUTENCAO');

-- 03.3 Movimentações determinísticas para 3 motos (1, 6, 11)
DECLARE
  v_base DATE := DATE '2025-09-29';
  v_setor_recepcao  CH_SETOR.ID_SETOR%TYPE;
  v_setor_entrega   CH_SETOR.ID_SETOR%TYPE;
  v_setor_comercial CH_SETOR.ID_SETOR%TYPE;
BEGIN
  SELECT id_setor INTO v_setor_recepcao  FROM ch_setor WHERE nome_setor = 'Recepcao';
  SELECT id_setor INTO v_setor_entrega   FROM ch_setor WHERE nome_setor = 'Entrega';
  SELECT id_setor INTO v_setor_comercial FROM ch_setor WHERE nome_setor = 'Comercial';

  -- Moto 1 – Recepcao / ENTRADA
  FOR k IN 0..14 LOOP
    INSERT INTO CH_MOVIMENTACAO (ID_MOTO, ID_SETOR, TIPO_MOV, DT_MOV, CUSTO, OBSERVACAO)
    VALUES (1, v_setor_recepcao, 'ENTRADA', v_base + k, 65 - k, 'Entrada padrão');
  END LOOP;

  -- Moto 6 – Entrega / TRANSF 
  FOR k IN 0..14 LOOP
    INSERT INTO CH_MOVIMENTACAO (ID_MOTO, ID_SETOR, TIPO_MOV, DT_MOV, CUSTO, OBSERVACAO)
    VALUES (6, v_setor_entrega, 'TRANSF', v_base + k, 130 - (2*k), 'Transferência de setor');
  END LOOP;

  -- Moto 11 – Comercial / SAIDA
  FOR k IN 0..14 LOOP
    INSERT INTO CH_MOVIMENTACAO (ID_MOTO, ID_SETOR, TIPO_MOV, DT_MOV, CUSTO, OBSERVACAO)
    VALUES (11, v_setor_comercial, 'SAIDA', v_base + k, 75 - (5*k), 'Saída para cliente');
  END LOOP;

  COMMIT;
END;
/

PROMPT ==== Conferência de carga  ====
COLUMN T FORMAT A15
SELECT 'CH_SETOR' T, COUNT(*) QTD FROM CH_SETOR
UNION ALL
SELECT 'CH_MOTOCICLETA', COUNT(*) FROM CH_MOTOCICLETA
UNION ALL
SELECT 'CH_MOVIMENTACAO', COUNT(*) FROM CH_MOVIMENTACAO;
PROMPT ================================================================

/* =============================== [04] PACKAGE REGRAS ================== */
CREATE OR REPLACE PACKAGE PKG_CH AS
  c_err_tipo_invalido    CONSTANT PLS_INTEGER := -20061;
  c_err_status_invalido  CONSTANT PLS_INTEGER := -20091;
  c_err_fk_inexistente   CONSTANT PLS_INTEGER := -20041;

  FUNCTION fun_valida_tipo(p_tipo VARCHAR2) RETURN NUMBER;
  FUNCTION fun_valida_status(p_status VARCHAR2) RETURN NUMBER;
  FUNCTION fun_soma_custo_moto(p_id_moto NUMBER) RETURN NUMBER;

  PROCEDURE prc_registrar_auditoria(
    p_tabela      IN VARCHAR2,
    p_operacao    IN VARCHAR2,
    p_id_registro IN NUMBER,
    p_detalhe     IN VARCHAR2
  );

  PROCEDURE prc_registrar_mov(
    p_id_moto   IN NUMBER,
    p_id_setor  IN NUMBER,
    p_tipo_mov  IN VARCHAR2,
    p_custo     IN NUMBER   DEFAULT 0,
    p_obs       IN VARCHAR2 DEFAULT NULL
  );

  PROCEDURE prc_atualiza_status_moto(
    p_id_moto     IN NUMBER,
    p_novo_status IN VARCHAR2
  );

  PROCEDURE prc_rel_somas;
END PKG_CH;
/

CREATE OR REPLACE PACKAGE BODY PKG_CH AS
  FUNCTION fun_valida_tipo(p_tipo VARCHAR2) RETURN NUMBER IS
  BEGIN
    IF UPPER(p_tipo) IN ('ENTRADA','SAIDA','TRANSF') THEN RETURN 1; ELSE RETURN 0; END IF;
  END;

  FUNCTION fun_valida_status(p_status VARCHAR2) RETURN NUMBER IS
  BEGIN
    IF UPPER(p_status) IN ('ATIVA','MANUTENCAO','INATIVA') THEN RETURN 1; ELSE RETURN 0; END IF;
  END;

  FUNCTION fun_soma_custo_moto(p_id_moto NUMBER) RETURN NUMBER IS
    v_total NUMBER := 0;
  BEGIN
    SELECT NVL(SUM(custo),0) INTO v_total FROM CH_MOVIMENTACAO WHERE id_moto = p_id_moto;
    RETURN v_total;
  EXCEPTION WHEN NO_DATA_FOUND THEN RETURN 0;
  END;

  PROCEDURE prc_registrar_auditoria(
    p_tabela      IN VARCHAR2,
    p_operacao    IN VARCHAR2,
    p_id_registro IN NUMBER,
    p_detalhe     IN VARCHAR2
  ) IS
  BEGIN
    INSERT INTO CH_AUDITORIA (tabela, operacao, id_registro, detalhe)
    VALUES (UPPER(p_tabela), UPPER(p_operacao), p_id_registro, p_detalhe);
  END;

  PROCEDURE prc_registrar_mov(
    p_id_moto   IN NUMBER,
    p_id_setor  IN NUMBER,
    p_tipo_mov  IN VARCHAR2,
    p_custo     IN NUMBER   DEFAULT 0,
    p_obs       IN VARCHAR2 DEFAULT NULL
  ) IS
    v_ok NUMBER; v_existe NUMBER; v_id_mov NUMBER;
  BEGIN
    -- FKs
    SELECT COUNT(*) INTO v_existe FROM CH_MOTOCICLETA WHERE id_moto = p_id_moto;
    IF v_existe = 0 THEN RAISE_APPLICATION_ERROR(c_err_fk_inexistente, 'Moto inexistente: '||p_id_moto); END IF;

    SELECT COUNT(*) INTO v_existe FROM CH_SETOR WHERE id_setor = p_id_setor;
    IF v_existe = 0 THEN RAISE_APPLICATION_ERROR(c_err_fk_inexistente, 'Setor inexistente: '||p_id_setor); END IF;

    -- validações básicas
    v_ok := fun_valida_tipo(p_tipo_mov);
    IF v_ok = 0 THEN RAISE_APPLICATION_ERROR(c_err_tipo_invalido, 'Tipo inválido: '||p_tipo_mov); END IF;
    IF p_custo < 0 THEN RAISE_APPLICATION_ERROR(-20011, 'Custo não pode ser negativo'); END IF;

    INSERT INTO CH_MOVIMENTACAO (id_moto, id_setor, tipo_mov, dt_mov, custo, observacao)
    VALUES (p_id_moto, p_id_setor, UPPER(p_tipo_mov), SYSDATE, p_custo, p_obs)
    RETURNING id_mov INTO v_id_mov;

    DBMS_OUTPUT.PUT_LINE('Movimentacao criada: ID_MOV='||v_id_mov);
    prc_registrar_auditoria('CH_MOVIMENTACAO','INSERT',v_id_mov,
      'Nova movimentacao - moto='||p_id_moto||
      ', setor='||p_id_setor||', tipo='||UPPER(p_tipo_mov)||
      ', custo='||TO_CHAR(p_custo,'FM9999990D00'));
  END;

  PROCEDURE prc_atualiza_status_moto(p_id_moto IN NUMBER, p_novo_status IN VARCHAR2) IS
    v_ok NUMBER; v_rows NUMBER;
  BEGIN
    v_ok := fun_valida_status(p_novo_status);
    IF v_ok = 0 THEN RAISE_APPLICATION_ERROR(c_err_status_invalido, 'Status inválido: '||p_novo_status); END IF;

    UPDATE CH_MOTOCICLETA SET status = UPPER(p_novo_status) WHERE id_moto = p_id_moto;
    v_rows := SQL%ROWCOUNT;
    IF v_rows = 0 THEN RAISE_APPLICATION_ERROR(c_err_fk_inexistente, 'Moto inexistente: '||p_id_moto); END IF;

    DBMS_OUTPUT.PUT_LINE('Status da moto '||p_id_moto||' atualizado para '||UPPER(p_novo_status));
    prc_registrar_auditoria('CH_MOTOCICLETA','UPDATE',p_id_moto,'STATUS='||UPPER(p_novo_status));
  END;

  PROCEDURE prc_rel_somas IS
    v_total_mov   NUMBER;
    v_total_custo NUMBER;
  BEGIN
    SELECT COUNT(*), NVL(SUM(custo),0) INTO v_total_mov, v_total_custo FROM CH_MOVIMENTACAO;
    DBMS_OUTPUT.PUT_LINE('Total de movimentacoes: '||v_total_mov);
    DBMS_OUTPUT.PUT_LINE('Soma de custos: '||TO_CHAR(v_total_custo,'FM9999990D00'));

    DBMS_OUTPUT.PUT_LINE('Top 5 motos por custo:');
    FOR r IN (
      SELECT m.id_moto, fun_soma_custo_moto(m.id_moto) soma
        FROM CH_MOTOCICLETA m
       ORDER BY soma DESC
       FETCH FIRST 5 ROWS ONLY
    ) LOOP
      DBMS_OUTPUT.PUT_LINE(' - Moto '||r.id_moto||': '||TO_CHAR(r.soma,'FM9999990D00'));
    END LOOP;
  END;
END PKG_CH;
/
PROMPT ..package regras ok

/* ========================= [05] WRAPPERS & TRIGGERS =================== */
CREATE OR REPLACE PROCEDURE PRC_CH_REGISTRAR_MOV(
  p_id_moto   IN NUMBER,
  p_id_setor  IN NUMBER,
  p_tipo_mov  IN VARCHAR2,
  p_custo     IN NUMBER   DEFAULT 0,
  p_obs       IN VARCHAR2 DEFAULT NULL
) AS
BEGIN
  PKG_CH.prc_registrar_mov(p_id_moto, p_id_setor, p_tipo_mov, p_custo, p_obs);
END;
/

CREATE OR REPLACE PROCEDURE PRC_CH_ATUALIZA_STATUS_MOTO(
  p_id_moto     IN NUMBER,
  p_novo_status IN VARCHAR2
) AS
BEGIN
  PKG_CH.prc_atualiza_status_moto(p_id_moto, p_novo_status);
END;
/

CREATE OR REPLACE PROCEDURE PRC_CH_REL_SOMAS AS
BEGIN
  PKG_CH.prc_rel_somas;
END;
/

-- trigger: PLACA sempre maiúscula
CREATE OR REPLACE TRIGGER TRG_CH_MOTO_PLACA_UP
BEFORE INSERT OR UPDATE OF PLACA ON CH_MOTOCICLETA
FOR EACH ROW
BEGIN
  :NEW.PLACA := UPPER(:NEW.PLACA);
END;
/

-- trigger: auditoria INSERT em MOV
CREATE OR REPLACE TRIGGER TRG_CH_MOV_AUD_INS
AFTER INSERT ON CH_MOVIMENTACAO
FOR EACH ROW
BEGIN
  PKG_CH.prc_registrar_auditoria(
    p_tabela      => 'CH_MOVIMENTACAO',
    p_operacao    => 'INSERT',
    p_id_registro => :NEW.ID_MOV,
    p_detalhe     => 'TRG: moto='||:NEW.ID_MOTO||
                     ', setor='||:NEW.ID_SETOR||
                     ', tipo='||:NEW.TIPO_MOV||
                     ', custo='||TO_CHAR(:NEW.CUSTO,'FM9999990D00')
  );
END;
/

-- trigger: auditoria UPDATE/DELETE em MOV
CREATE OR REPLACE TRIGGER TRG_CH_MOV_AUD_UPDDEL
AFTER UPDATE OR DELETE ON CH_MOVIMENTACAO
FOR EACH ROW
DECLARE
  v_op  VARCHAR2(10);
  v_id  NUMBER;
  v_det VARCHAR2(4000);
BEGIN
  IF UPDATING THEN
    v_op := 'UPDATE';
    v_id := :NEW.ID_MOV;
    v_det := 'TRG: id='||:NEW.ID_MOV||' custo: '||
             NVL(TO_CHAR(:OLD.CUSTO,'FM9999990D00'),'0')||'->'||
             NVL(TO_CHAR(:NEW.CUSTO,'FM9999990D00'),'0');
  ELSIF DELETING THEN
    v_op := 'DELETE';
    v_id := :OLD.ID_MOV;
    v_det := 'TRG: id='||:OLD.ID_MOV||' (removido)';
  END IF;

  PKG_CH.prc_registrar_auditoria('CH_MOVIMENTACAO', v_op, v_id, v_det);
END;
/

-- trigger: auditoria ao mudar STATUS da moto
CREATE OR REPLACE TRIGGER TRG_CH_MOTO_STATUS_AUD
AFTER UPDATE OF STATUS ON CH_MOTOCICLETA
FOR EACH ROW
BEGIN
  PKG_CH.prc_registrar_auditoria(
    p_tabela      => 'CH_MOTOCICLETA',
    p_operacao    => 'UPDATE',
    p_id_registro => :NEW.ID_MOTO,
    p_detalhe     => 'TRG: STATUS '||:OLD.STATUS||'->'||:NEW.STATUS
  );
END;
/
PROMPT ..wrappers e triggers ok

/* ============================= [06] EXPORT JSON =======================
   JSON manual, gravado em CH_JSON_OUT
   ===================================================================== */
CREATE TABLE CH_JSON_OUT (
  ORD   NUMBER,
  LINHA VARCHAR2(4000)
);

CREATE OR REPLACE PACKAGE PKG_CH_JSON AS
  PROCEDURE prc_gerar_json_mov_det;
END PKG_CH_JSON;
/

CREATE OR REPLACE PACKAGE BODY PKG_CH_JSON AS
  FUNCTION fmt_num(p_n NUMBER) RETURN VARCHAR2 IS
  BEGIN
    RETURN TO_CHAR(p_n,'FM9999990D00','NLS_NUMERIC_CHARACTERS=.,');
  END;
  PROCEDURE prc_gerar_json_mov_det IS
    v_ord   NUMBER := 0;
    v_first BOOLEAN := TRUE;
    v_count NUMBER;

    PROCEDURE out_line(p VARCHAR2) IS
    BEGIN
      v_ord := v_ord + 1;
      INSERT INTO CH_JSON_OUT(ORD, LINHA) VALUES (v_ord, p);
    END;
  BEGIN
    DELETE FROM CH_JSON_OUT;

    out_line('[');

    FOR r IN (
      SELECT m.id_moto,
             s.nome_setor AS setor,
             m.tipo_mov   AS tipo,
             TO_CHAR(m.dt_mov,'YYYY-MM-DD"T"HH24:MI:SS') AS dt_mov,
             NVL(m.observacao,'') AS obs,
             m.custo
        FROM CH_MOVIMENTACAO m
        JOIN CH_SETOR s ON s.id_setor = m.id_setor
       WHERE m.id_moto IN (1,6,11)                      
       ORDER BY m.id_moto, m.dt_mov
    ) LOOP
      IF v_first THEN
        v_first := FALSE;
      ELSE
        out_line(','); 
      END IF;

      out_line(
        '{'||
        '"id_moto":'||r.id_moto||','||
        '"setor":"'||REPLACE(r.setor,'"','""')||'",'||
        '"tipo":"'||REPLACE(r.tipo ,'"','""')||'",'||
        '"dt_mov":"'||r.dt_mov||'",'||
        '"obs":"'||REPLACE(r.obs  ,'"','""')||'",'||
        '"custo":'||fmt_num(r.custo)||
        '}'
      );
    END LOOP;

    out_line(']');

    SELECT COUNT(*) INTO v_count FROM CH_JSON_OUT;
    DBMS_OUTPUT.PUT_LINE('JSON gerado em CH_JSON_OUT com '||v_count||' linhas.');
    COMMIT;
  END prc_gerar_json_mov_det;
END PKG_CH_JSON;
/
PROMPT ..export ok

BEGIN
  PKG_CH_JSON.prc_gerar_json_mov_det;
END;
/

-- Preview e contagem
PROMPT ==== Preview CH_JSON_OUT 
SELECT * FROM CH_JSON_OUT WHERE ORD <= 20 ORDER BY ORD;

PROMPT ==== Total de linhas 
SELECT COUNT(*) AS QTD_LINHAS FROM CH_JSON_OUT;

-- ========================= [07] SPOOL (OPCIONAL) ======================
-- escolha UM dos blocos abaixo (remova os "--" para habilitar):


-- (7) SPOOL simples no diretório atual
SET SQLFORMAT JSON
SET LONG 1000000 LONGCHUNKSIZE 1000000
SET PAGES 0 TRIMSPOOL ON LINESIZE 32767
-- Dica: garantir UTF-8 no cliente
-- Windows: set NLS_LANG=.AL32UTF8   (na mesma janela)
-- PowerShell: chcp 65001
SPOOL ch_movimentos.json
SELECT LINHA FROM CH_JSON_OUT ORDER BY ORD;
SPOOL OFF

PROMPT == FIM ==
